expression = logic

logic = logic && relational
    | logic || relational
    | relational

relational = relational == bitwise
    | relational != bitwise
    | relational < bitwise
    | relational <= bitwise
    | relational > bitwise
    | relational >= bitwise
    | bitwise

bitwise = bitwise & arithmetic
    | bitwise | arithmetic
    | bitwise ^ arithmetic
    | bitwise << arithmetic
    | bitwise >> arithmetic
    | arithmetic

arithmetic = arithmetic + multiplicative
    | arithmetic - multiplicative
    | multiplicative

multiplicative = multiplicative * unary
    | multiplicative / unary
    | multiplicative % unary
    | unary

unary = - exponential
    | ! exponential
    | ~ exponential
    | exponential

exponential = atom ** exponential
    | atom

atom = INT
    | FLOAT
    | BOOLEAN
    | STRING
    | ( expression )
    | [ expression , expression ]
    | # [ expression , expression ]
    | MAX ( [ expression , expression ] , [ expression , expression ] )
    | MIN ( [ expression , expression ] , [ expression , expression ] )
    | MEAN ( [ expression , expression ] , [ expression , expression ] )
    | SUM ( [ expression , expression ] , [ expression , expression ] )
    | FLOAT_CAST ( expression )
    | INT_CAST (expression )